(* 
      This file reads in lib/pow-reed.nb and uses the definitions there to generate a highlighted graph
      showing betweenness centrality for the REED graph to be used by REED.tex

      This depends on running reed -m lib/pow-reed to generate the Mathematia file lib/pow-reed.nb
*)

pad = "     ";

SetDirectory[NotebookDirectory[]];
Print["Running Mathematica in ", Directory[]];
dataFile = "lib/pow-reed.nb";

Print["Make betweenness graph used by reed.tex paper\n", pad, dataFile, " defines:"];

Get[dataFile];

nameExpand = Map[("name" -> "nodes") /. # &, groups];

(* anything that goes to or comes from a group goes to all or comes from all members of the group *)
expandRule[u_ -> v_] := 
 Module[{ul = u /. nameExpand, vl = v /. nameExpand},
  Outer[Rule,
    If[Head[ul] === List, ul, {ul}], 
    If[Head[vl] === List, vl, {vl}]
  ]
];

expandedEdges = Flatten@Map[expandRule, edges];

(* make the expanded edges dashed or dotted, whatever *)
fixCompoundEdges[u_ -> v_] := 
   Module[{ul = u /. nameExpand, vl = v /. nameExpand},
   If[Head[ul] === List || Head[vl] === List, List, zap]@Flatten@Outer[Rule,
          If[Head[ul] === List, ul, {ul}], 
          If[Head[vl] === List, vl, {vl}]
        ]
   ];
   
arrowSize = 0.015;
arrowColor = Gray;

styledEdges = (# -> Directive[Opacity[1], arrowColor, Thick, Dashed, Arrowheads[arrowSize]]) & /@ DeleteCases[Flatten@Map[fixCompoundEdges, edges], zap@_];
PrependTo[styledEdges, _ -> Directive[Opacity[1],arrowColor, Thick, Arrowheads[arrowSize]]];

g = Graph@expandedEdges;

bg = Map[
         Module[{b, g, vSize},
                g = Subgraph[expandedEdges,#];
                b = BetweennessCentrality[g];
                vSize = Thread[VertexList[g] -> Rescale[Rescale[b], {0, 1}, {If[Length@b == 3, .15, .2], 2}]];
                Graph[g,
                      VertexStyle -> Map[# -> Darker[Red]&, VertexList[g]],
                      EdgeStyle -> styledEdges,
                      EdgeShapeFunction -> Function[{path,endPoints},
                                                    {Arrowheads[If[Length@b==3, .15, .0175]],
                                                     Arrow[path, {0,(endPoints[[2]]/.vSize)/2}]}
                                                   ],
                      VertexSize -> vSize,
                      VertexLabels -> {"7"->Placed[Style[Column[{"PrecisionWeb","database"},Center],24,Bold,Black],{0.55,1.25}]}
                     ]
         ]&,
            WeaklyConnectedComponents[expandedEdges]
        ];

betweennessplot = Grid[{{Show[bg[[1]], ImageSize->1000], Show[bg[[2]], ImageSize->200]}}];

format = "jpg";
 	
If[True,
    Print["Exported: ", Export["betweenness1."<>format, Show[bg[[1]], ImageSize->1000]]];
	Print["Exported: ", Export["betweenness2."<>format, Show[bg[[2]], ImageSize->200]]],
	
	betweennessplot
]